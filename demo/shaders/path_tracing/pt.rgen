#version 460

#extension GL_EXT_ray_tracing : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require

#include "../shader_common.glsl"
#include "../raytracing_common.glsl"

#define ENABLE_ANTI_ALIASING 1

layout(location = 0) rayPayloadEXT PTRayPayload prd;

layout(set = 0, binding = 0) uniform CAMERA { Camera camera; };

layout(set = 1, binding = 0) uniform accelerationStructureEXT TLAS;
layout(set = 1, binding = 1, rgba32f) uniform writeonly image2D PathTracingOut;
layout(set = 1, binding = 2, r32f) uniform writeonly image2D PathTracingDepthOut;

layout(push_constant) uniform FRAME_INFO { FrameInfo frameInfo; };

void main()
{
	const uint launchIndex = gl_LaunchIDEXT.x + gl_LaunchIDEXT.y * gl_LaunchSizeEXT.x;

	// init payload
	prd.seed = initSeed(launchIndex + frameInfo.frameIndex * 1799);
	prd.traceDepth = 0;
	prd.rayMask = generateRayMask(prd.seed);
	prd.energy = vec3(0);
	prd.transmission = vec3(1);

	// Calculate pixel center & location
	vec2 pixelLocation = gl_LaunchIDEXT.xy;
	vec2 pixelCenter = pixelLocation + vec2(0.5);

	vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
	mat4 invProj = inverse(camera.project);
	mat4 invView = inverse(camera.view);
	vec2 ndc = inUV * 2.0 - 1.0;
	
	vec4 rayDirection = invProj * vec4(ndc, 1, 1);
	vec3 wPos = vec3(invView * vec4(0, 0, 0, 1));
	vec3 Wo = vec3(invView * vec4(normalize(rayDirection.xyz), 0));

	traceRayEXT(
		TLAS,
		gl_RayFlagsOpaqueEXT,
		prd.rayMask,
		0, 0, 0,
		wPos,
		RAYTRACE_RANGE_TMIN,
		Wo,
		RAYTRACE_RANGE_TMAX,
		0
	);
	
	vec4 screenPos = camera.project * camera.view * vec4(prd.hitPos, 1);
	screenPos = vec4(screenPos.xyz / screenPos.w, 1);
	float depth = screenPos.z;

	vec4 outColor = vec4(prd.energy, 1);
	imageStore(PathTracingOut, ivec2(gl_LaunchIDEXT.xy), outColor);
	imageStore(PathTracingDepthOut, ivec2(gl_LaunchIDEXT.xy), vec4(depth));
}
